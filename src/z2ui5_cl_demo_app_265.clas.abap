CLASS z2ui5_cl_demo_app_265 DEFINITION
  PUBLIC
  CREATE PUBLIC.

PUBLIC SECTION.

  INTERFACES z2ui5_if_app.

  DATA check_initialized TYPE abap_bool.
  PROTECTED SECTION.

    DATA client TYPE REF TO z2ui5_if_client.

    METHODS display_view
      IMPORTING
        client TYPE REF TO z2ui5_if_client.
    METHODS on_event
      IMPORTING
        client TYPE REF TO z2ui5_if_client.
    METHODS z2ui5_display_popover
      IMPORTING
        id TYPE string.

  PRIVATE SECTION.
ENDCLASS.



CLASS Z2UI5_CL_DEMO_APP_265 IMPLEMENTATION.


  METHOD display_view.

    DATA(page) = z2ui5_cl_xml_view=>factory( )->shell(
         )->page(
            title          = 'abap2UI5 - Sample: Code Editor'
            navbuttonpress = client->_event( 'BACK' )
            shownavbutton  = xsdbool( client->get( )-s_draft-id_prev_app_stack IS NOT INITIAL ) ).

    page->header_content(
       )->button( id = `button_hint_id`
           icon = `sap-icon://hint`
           tooltip = `Sample information`
           press = client->_event( 'CLICK_HINT_ICON' ) ).

    page->header_content(
       )->link(
           text   = 'UI5 Demo Kit'
           target = '_blank'
           href   = 'https://sapui5.hana.ondemand.com/sdk#/entity/sap.ui.codeeditor.CodeEditor/sample/sap.ui.codeeditor.sample.CodeEditor' ).

    page->code_editor(
             type = `json`
             value = `\{  `                     &&
             ` "Chinese" : "你好世界",  `       &&
             ` "Dutch" : "Hallo wereld",  `     &&
             ` "English" : "Hello world",  `    &&
             ` "French" : "Bonjour monde",  `   &&
             ` "German" : "Hallo Welt",  `      &&
             ` "Greek" : "γειά σου κόσμος",  `  &&
             ` "Italian" : "Ciao mondo",  `     &&
             ` "Japanese" : "こんにちは世界",  `   &&
             ` "Korean" : "여보세요 세계",  `        &&
             ` "Portuguese" : "Olá mundo",  `   &&
             ` "Russian" : "Здравствуй мир",  ` &&
             ` "Spanish" : "Hola mundo"  `      &&
          `}`
          height = `300px` ).

    client->view_display( page->stringify( ) ).

  ENDMETHOD.


  METHOD on_event.

    CASE client->get( )-event.
      WHEN 'BACK'.
        client->nav_app_leave( ).
      WHEN 'CLICK_HINT_ICON'.
        z2ui5_display_popover( `button_hint_id` ).
    ENDCASE.

  ENDMETHOD.


  METHOD z2ui5_display_popover.

    DATA(view) = z2ui5_cl_xml_view=>factory_popup( ).
    view->quick_view( placement = `Bottom` width = `auto`
              )->quick_view_page( pageid = `sampleInformationId`
                                  header = `Sample information`
                                  description = `Display or edit source code with syntax highlighting for various source types.` ).

    client->popover_display(
      xml   = view->stringify( )
      by_id = id
    ).

  ENDMETHOD.


  METHOD z2ui5_if_app~main.

    me->client = client.

    IF check_initialized = abap_false.
      check_initialized = abap_true.
      display_view( client ).
    ENDIF.

    on_event( client ).

  ENDMETHOD.
ENDCLASS.
